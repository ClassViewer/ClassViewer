group 'org.glavo'
version '3.3-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

if(sourceCompatibility.isJava9Compatible()) {
    sourceSets.main.java.srcDirs += 'src/main/java9'
}

repositories {
    mavenCentral()
}

dependencies{
    compile "de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.5"
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Implementation-Version': '1.2',
                'Main-Class': 'org.glavo.viewer.gui.Viewer'
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"

import org.apache.tools.ant.filters.EscapeUnicode

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(EscapeUnicode)
    }
}

task run(type: JavaExec, dependsOn: jar) {
    main = "-jar"
    args jar.archivePath
}

def java_home = System.getProperty("java.home")

task link(type: Exec) {
    dependsOn clean
    dependsOn jar
    workingDir 'build'

    commandLine "${java_home}/bin/jlink",
            '--no-header-files',
            '--no-man-pages',
            '--module-path', "${jar.archivePath}${File.pathSeparatorChar}${java_home}/jmods",
            '--add-modules', 'org.glavo.viewer',
            '--launcher', 'viewer=org.glavo.viewer/org.glavo.viewer.gui.Viewer',
            '--output', "ClassViewer-${version}"
}